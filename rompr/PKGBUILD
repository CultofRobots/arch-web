# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots
# Based on PKGBUILD from AUR
# Contributor: Mark Collins < tera_1225 [at] hotmail [dot] com>
# Contributor: gls <ghostlovescorebg [at] gmail [dot] com>

pkgname=rompr
pkgver=2.24
pkgrel=1
pkgdesc="A php web frontend to MPD and Mopidy"
arch=('x86_64' 'armv7h' 'aarch64')
url='https://github.com/fatg3erman/RompR/'
license=('LicenseRef-rompr')
depends=(
  'imagemagick'
  'php' # php-json, php-curl, php-xml and php-mbstring are included in php
  'php-sqlite'
  'php-gd'
  'php-intl'
  'python'
  'python-websockets'
)
conflicts=('rompr<2.00')
optdepends=(
  'apache: webserver to serve app'
  'nginx: webserver to serve app'
  'mysql: alternate database platform to sqlite'
)
source=("${pkgname}-${pkgver}.zip::https://github.com/fatg3erman/RompR/releases/download/${pkgver}/${pkgname}-${pkgver}.zip")
sha256sums=('8c19ad2dc414183ae7da446c174f9d27824ef7488be1fb3772c4e2a0983c9117')
install="${pkgname}.install"
backup=(
  "var/lib/${pkgname}/albumart"
  "var/lib/${pkgname}/prefs"
)
options=('emptydirs')

package() {
  cd "${srcdir}"
  find -name '.svn' -delete
  install -d "${pkgdir}/usr/share/webapps"
  cp -ra "$pkgname" "${pkgdir}/usr/share/webapps/"
  install -d -m 755 ${pkgdir}/var/lib/${pkgname}/{albumart,prefs}
  ln -s "/var/lib/${pkgname}/albumart" "${pkgdir}/usr/share/webapps/${pkgname}/albumart"
  ln -s "/var/lib/${pkgname}/prefs" "${pkgdir}/usr/share/webapps/${pkgname}/prefs"
  install -d "${pkgdir}/usr/share/licenses/$pkgname"
  mv "${pkgdir}/usr/share/webapps/${pkgname}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots/
# Based on PKGBUILD from AUR
# Contributor: max-k <max-k [at] post [dot] com>

pkgname=ampache
pkgver=7.4.2
pkgrel=1
pkgdesc="PHP web based audio/video streaming application and file manager"
arch=('x86_64' 'armv7h' 'aarch64')
url="http://www.ampache.org/"
license=('GPL')
_phpver=8.4
depends=(
  'mariadb>=10.0'
  "php>=${_phpver}"
)
optdepends=(
  'lame: all transcoding/downsampling'
  'vorbis-tools: all transcoding'
  'flac: flac transcoding/downsampling'
  'faad2: m4a transcoding/downsampling'
  'mp3splt: mp3 and ogg transcoding/downsampling'
)
conflicts=(
  'ampache-git'
  'ampache-development'
)
backup=("etc/webapps/$pkgname/ampache.cfg.php")
install="${pkgname}.install"
source=("https://github.com/ampache/ampache/releases/download/${pkgver}/${pkgname}-${pkgver}_all_php${_phpver}.zip"
        "nginx-example.conf"
        "${pkgname}.install"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles")
sha256sums=('9f839a8b4f0f7a793ae30c8e4aed6893fb46b127dc402fc747f79cffe82274f9'
            'f5e9ae092c73706a21d73bc76c9d0b405c8f592064d7ac96b20653ac39c1acd4'
            '44b4f523be305532a9532425cad4c880b42cf7ef925664ab1ea6b1f74015b84f'
            'aa1fa320703a4020cdc673e1c35a33a41158e7fb687f7c275065c424b98f9d12'
            '72562d933003c99cd2447469395fa52033dcedc86091063a7eeac3dd22e5f09e')
options=(!strip)

package() {
  cd "$srcdir" || exit 1
  _targetdir="${pkgdir}/usr/share/webapps/${pkgname}"
  _docdir="${pkgdir}/usr/share/doc/${pkgname}"
  _mandir="${pkgdir}/usr/share/man/man1"
  _vendordir="${_targetdir}/lib/vendor"


  mkdir -p "$_targetdir"
  cp -r ./* "${_targetdir}/"
  unlink "${_targetdir}/${pkgname}-${pkgver}_all_php${_phpver}.zip"
  unlink "${_targetdir}/nginx-example.conf"
  unlink "${_targetdir}/ampache.install"
  unlink "${_targetdir}/${pkgname}.sysusers"
  unlink "${_targetdir}/${pkgname}.tmpfiles"
  rm -r "${_targetdir}/docs/man"
  mkdir -p "$_docdir"
  mkdir -p "$_mandir"
  install -D -m644 "${srcdir}/docs/man/man1/ampache.1" "${_mandir}/"
  install -D -m644 "${srcdir}/nginx-example.conf" "${_docdir}/"
  find "$_targetdir" -type d -exec chmod 755 {} \;
  find "$_docdir" -type d -exec chmod +x {} \;
  find "$_vendordir" -type d -name '.git' -exec rm -r {} 2>/dev/null \; || true

  mkdir -p "$pkgdir/etc/webapps/"
  mv "${pkgdir}/usr/share/webapps/${pkgname}/config" "$pkgdir/etc/webapps/ampache"
  ln -s "/etc/webapps/$pkgname" "$pkgdir/usr/share/webapps/$pkgname/config"

  install -Dm644 ${pkgname}.sysusers "$pkgdir/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm644 ${pkgname}.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/${pkgname}.conf"
}

# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots/
# Based on PKGBUILD from AUR
# Contributor: max-k <max-k [at] post [dot] com>

pkgname=ampache
pkgver=6.0.2
pkgrel=1
pkgdesc="PHP web based audio/video streaming application and file manager"
arch=('x86_64' 'armv7h' 'aarch64')
url="http://www.ampache.org/"
license=('GPL')
depends=(
  'mariadb>=10.0'
  'php>=8.2'
)
optdepends=(
  'lame: all transcoding/downsampling'
  'vorbis-tools: all transcoding'
  'flac: flac transcoding/downsampling'
  'faad2: m4a transcoding/downsampling'
  'mp3splt: mp3 and ogg transcoding/downsampling'
)
conflicts=(
  'ampache-git'
  'ampache-development'
)
install="${pkgname}.install"
source=("https://github.com/ampache/ampache/releases/download/${pkgver}/${pkgname}-${pkgver}_all_php8.2.zip"
        "nginx-example.conf"
        "${pkgname}.install"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles")
sha256sums=('3e36fdcda1b5fb135ab28e5b02a7a9f26f912b49357a2db1962207c006b92e7c'
            'f5e9ae092c73706a21d73bc76c9d0b405c8f592064d7ac96b20653ac39c1acd4'
            '6d7849234af351b481aa6546debe94952a99a5accd5f37b8d068baf1ecfd0b35'
            'aa1fa320703a4020cdc673e1c35a33a41158e7fb687f7c275065c424b98f9d12'
            '8da7e7effb5b0b5161f1592c4a3ab14fda832af2b610eddc396f0b21ed4151e9')
options=(!strip)

build() {
  echo "" > /dev/null
}

package() {
  cd "$srcdir" || exit 1
  _targetdir="${pkgdir}/usr/share/webapps/${pkgname}"
  _docdir="${pkgdir}/usr/share/doc/${pkgname}"
  _mandir="${pkgdir}/usr/share/man/man1"
  _vendordir="${_targetdir}/lib/vendor"
  mkdir -p "$_targetdir"
  cp -r ./* "${_targetdir}/"
  unlink "${_targetdir}/${pkgname}-${pkgver}_all_php8.2.zip"
  unlink "${_targetdir}/nginx-example.conf"
  unlink "${_targetdir}/ampache.install"
  unlink "${_targetdir}/${pkgname}.sysusers"
  unlink "${_targetdir}/${pkgname}.tmpfiles"
  rm -r "${_targetdir}/docs/man"
  mkdir -p "$_docdir"
  mkdir -p "$_mandir"
  install -D -m644 "${srcdir}/docs/man/man1/ampache.1" "${_mandir}/"
  install -D -m644 "${srcdir}/nginx-example.conf" "${_docdir}/"
  find "$_targetdir" -type d -exec chmod 755 {} \;
  find "$_docdir" -type d -exec chmod +x {} \;
  find "$_vendordir" -type d -name '.git' -exec rm -r {} 2>/dev/null \; || true
 
  install -Dm644 ${pkgname}.sysusers "$pkgdir/usr/lib/sysusers.d/${pkgname}.conf"
  install -Dm644 ${pkgname}.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/${pkgname}.conf"
}

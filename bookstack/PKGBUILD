# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots/
# Based on PKGBUILD from AUR
# Contributor: Spencer Muise <smuise [at] spencermuise [dot] ca>
# Contributor: MartinRys <spleefer90 [at] gmail [dot] com> # bookstack.hook

pkgname=bookstack
_pkgname=BookStack
pkgver=23.08
pkgrel=1
pkgdesc='A simple, self-hosted, easy-to-use platform for organising and storing information'
arch=('x86_64' 'armv7h' 'aarch64')
url="https://github.com/BookStackApp/BookStack"
license=('MIT')
depends=('php' 'php-gd' 'php-fpm')
makedepends=('composer')
options=("!strip")
backup=("etc/webapps/$pkgname/config.env")
source=("$pkgname-$pkgver.tar.gz::https://github.com/BookStackApp/BookStack/archive/v${pkgver}.tar.gz"
        "$pkgname.hook")
sha256sums=('47e4d5c7ff58d83fd18a9e63d8cffcdf6f8549a1c176b259c794d08eced12a2d'
            '65bfc4fe0d8c615504d766e3130a5c91e6e55eb997e0102785ea83e4cdc6f7b4')

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  COMPOSER_ALLOW_SUPERUSER=1 composer install --no-dev --ignore-platform-reqs --no-interaction

  install -d "$pkgdir/usr/share/webapps/$pkgname" "$pkgdir/usr/share/licenses/$pkgname" "$pkgdir/etc/webapps/$pkgname"
  cp -r * "$pkgdir/usr/share/webapps/$pkgname"
  install -D "$srcdir/$_pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"

  cp .env.example "$pkgdir/etc/webapps/$pkgname/config.env"

  ln -s "/etc/webapps/$pkgname/config.env" "$pkgdir/usr/share/webapps/$pkgname/.env"
  rm -rf "$pkgdir/usr/share/webapps/$pkgname/bootstrap/cache"

  mkdir -p "$pkgdir/var/cache/$pkgname"
  chown http:http "$pkgdir/var/cache/$pkgname"
  ln -s "/var/cache/$pkgname" "$pkgdir/usr/share/webapps/$pkgname/bootstrap/cache"

  mkdir -p "$pkgdir/var/lib"
  mv "$pkgdir/usr/share/webapps/$pkgname/storage" "$pkgdir/var/lib/$pkgname"
  chown -R http:http "$pkgdir/var/lib/$pkgname"
  chmod 775 "$pkgdir/var/lib/$pkgname"
  ln -s "/var/lib/$pkgname" "$pkgdir/usr/share/webapps/$pkgname/storage"

  install -vDm 644 "../${pkgname}.hook" "${pkgdir}/etc/pacman.d/hooks/${pkgname}.hook"
}

# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots/
# Based on PKGBUILD from AUR
# Contributor: MartinRys <rys [dot] rs/contact
# Contributor: Spencer Muise <smuise [at] spencermuise [dot] ca>

pkgname=bookstack
_pkgname=BookStack
pkgver=24.12.1
pkgrel=1
pkgdesc='A simple, self-hosted, easy-to-use platform for organising and storing information'
arch=('x86_64' 'armv7h' 'aarch64')
url="https://github.com/BookStackApp/BookStack"
license=('MIT')
depends=(
  'php>=8.1'
  'php-gd'
)
optdepends=(
  'php-apache: webserver and a PHP implementation'
  'php-fpm: fastCGI'
  'mariadb: database'
  'nginx: webserver'
 )
makedepends=('composer')
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/BookStackApp/BookStack/archive/v${pkgver}.tar.gz"
  "$pkgname.hook"
  "$pkgname.conf"
)
sha256sums=(
  '94303fbe47e202225c22ec45d67617cf9de0a17a576f973426435cd73cc4e64d'
  '65bfc4fe0d8c615504d766e3130a5c91e6e55eb997e0102785ea83e4cdc6f7b4'
  '82152fc91366d1bd623c559c03240f070e08d45a084a4c6e2e211f671bee8c78'
)
options=('!strip')
backup=("etc/webapps/$pkgname/config.env")

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  COMPOSER_ALLOW_SUPERUSER=1 composer install --no-dev --ignore-platform-reqs --no-interaction

  install -d "$pkgdir/usr/share/webapps/$pkgname" "$pkgdir/usr/share/licenses/$pkgname" "$pkgdir/etc/webapps/$pkgname"
  cp -r * "$pkgdir/usr/share/webapps/$pkgname"
  install -D "$srcdir/$_pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"

  cp .env.example "$pkgdir/etc/webapps/$pkgname/config.env"

  ln -s "/etc/webapps/$pkgname/config.env" "$pkgdir/usr/share/webapps/$pkgname/.env"
  rm -rf "$pkgdir/usr/share/webapps/$pkgname/bootstrap/cache"

  mkdir -p "$pkgdir/var/cache/$pkgname"
  ln -s "/var/cache/$pkgname" "$pkgdir/usr/share/webapps/$pkgname/bootstrap/cache"

  mkdir -p "$pkgdir/var/lib"
  mv "$pkgdir/usr/share/webapps/$pkgname/storage" "$pkgdir/var/lib/$pkgname"
  ln -s "/var/lib/$pkgname" "$pkgdir/usr/share/webapps/$pkgname/storage"

  install -vDm 644 "${srcdir}/${pkgname}.hook" "${pkgdir}/etc/pacman.d/hooks/${pkgname}.hook"
  install -vDm 644 "${srcdir}/${pkgname}.conf" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}

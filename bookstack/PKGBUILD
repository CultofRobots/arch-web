# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots/
# Based on PKGBUILD from AUR
# Contributor: Spencer Muise <smuise [at] spencermuise [dot] ca>

pkgname=bookstack
_pkgname=BookStack
pkgver=21.05.3
pkgrel=1
pkgdesc='A simple, self-hosted, easy-to-use platform for organising and storing information'
arch=('x86_64' 'armv7h' 'aarch64')
url="https://github.com/BookStackApp/BookStack"
license=('MIT')
depends=('php' 'php-gd' 'php-fpm')
makedepends=('composer')
install="$pkgname.install"
options=("!strip")
backup=("etc/webapps/$pkgname/config.env")
source=("$pkgname-$pkgver.tar.gz::https://github.com/BookStackApp/BookStack/archive/v${pkgver}.tar.gz"
        "$pkgname.install")
sha256sums=('a83e2a30ebf58e2bf6d3f0581c7be2cd94d9db531532d1862bc61b6f60f50c0d'
            '35181f026dcabb215f15ba9d3406373b82db946755f4e2f30c4bc96de0ce5834')

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  composer install --no-scripts --no-dev --ignore-platform-reqs --no-interaction --optimize-autoloader

  install -d "$pkgdir/usr/share/webapps/$pkgname" "$pkgdir/usr/share/licenses/$pkgname" "$pkgdir/etc/webapps/$pkgname"
  cp -r * "$pkgdir/usr/share/webapps/$pkgname"
  install -D "$srcdir/$_pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"

  cp .env.example "$pkgdir/etc/webapps/$pkgname/config.env"

  ln -s "/etc/webapps/$pkgname/config.env" "$pkgdir/usr/share/webapps/$pkgname/.env"
  rm -rf "$pkgdir/usr/share/webapps/$pkgname/bootstrap/cache"

  mkdir -p "$pkgdir/var/cache/$pkgname"
  chown http:http "$pkgdir/var/cache/$pkgname"
  ln -s "/var/cache/$pkgname" "$pkgdir/usr/share/webapps/$pkgname/bootstrap/cache"

  mkdir -p "$pkgdir/var/lib"
  mv "$pkgdir/usr/share/webapps/$pkgname/storage" "$pkgdir/var/lib/$pkgname"
  chown -R http:http "$pkgdir/var/lib/$pkgname"
  chmod 775 "$pkgdir/var/lib/$pkgname"
  ln -s "/var/lib/$pkgname" "$pkgdir/usr/share/webapps/$pkgname/storage"
}

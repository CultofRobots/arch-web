# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots/
# Based on PKGBUILD from AUR
# Contributor: Spencer Muise <smuise [at] spencermuise [dot] ca>

pkgname=calibre-web
pkgver=0.6.24
pkgrel=2
pkgdesc="Web app for browsing, reading and downloading eBooks stored in a Calibre database"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://github.com/janeczku/calibre-web"
license=('GPL3')
depends=(
  'file'        
  'python'
  'python-apscheduler'
  'python-babel'
  'python-bleach'
  'python-chardet'
  'python-cryptography'
  'python-flask'
  'python-flask-babel'
  'python-flask-httpauth'
  'python-flask-limiter'
  'python-flask-principal'
  'python-flask-wtf'
  'python-lxml'
  'python-magic'
  'python-netifaces-plus'
  'python-pycountry'
  'python-pypdf'
  'python-pytz'
  'python-regex'
  'python-requests'
  'python-sqlalchemy'
  'python-tornado'
  'python-unidecode'
  'python-urllib3'
  'python-wand'
)
optdepends=(
  'python-jsonschema: kobo sync support'
)
makedepends=(
  'python-setuptools'
  'python-setuptools-scm'
)
provides=('calibre-web')
install=calibre-web.install
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/janeczku/calibre-web/archive/${pkgver}.tar.gz"
  'sqlalchemy.patch'
  'calibre-web.install'
  'calibre-web.service'
  'calibre-web.sysusers'
  'calibre-web.tmpfiles'
)
sha256sums=(
  '65d06505765e9a48c02451437e39afab3e7deb7bb13a705359ea271e32990cc8'
  'e109ad7b1778582f740b38148c4518cc556f3e032efa3b2e9b6ff2af19a70f31'
  '91352616c004ab08590e892b1f8a74cf53fda876254699e3126021f557ea0231'
  'f2072ddcbd5753b75272b90d571c49b665506705d8da682210be64f908708e2c'
  'b5d6c225761244b6c698af0279c9501bb9514e3e2a5151ca6e3212a4f5d0d76a'
  '82fae88cbed6d69e5a04586293de81bdc55994a4fe003178c39b5a35f2c87c1a'
)

prepare() {
  patch -d $pkgname-$pkgver -Np0 -i ../sqlalchemy.patch
}

package() {
  cd $pkgname-$pkgver
  install -d -m 755 "${pkgdir}/usr/share/webapps/calibre-web"
  cp -a "${srcdir}/$pkgname-$pkgver"/* "${pkgdir}/usr/share/webapps/calibre-web/"
  rm -rf "${pkgdir}/usr/share/webapps/calibre-web/.git"

  install -D -m 644 "${srcdir}/calibre-web.service" "${pkgdir}/usr/lib/systemd/system/calibre-web.service"
  install -D -m 644 "${srcdir}/calibre-web.sysusers" "${pkgdir}/usr/lib/sysusers.d/calibre-web.conf"
  install -D -m 644 "${srcdir}/calibre-web.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/calibre-web.conf"
}

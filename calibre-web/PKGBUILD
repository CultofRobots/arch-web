# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots/
# Based on PKGBUILD from AUR
# Contributor: Spencer Muise <smuise [at] spencermuise [dot] ca>

pkgname=calibre-web
pkgver=0.6.21
pkgrel=3
pkgdesc="Web app for browsing, reading and downloading eBooks stored in a Calibre database"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://github.com/janeczku/calibre-web"
license=('GPL3')
depends=(
  'python'
  'python-advocate'
  'python-apscheduler'
  'python-babel'
  'python-chardet'
  'python-cryptography'
  'python-flask'
  'python-flask-babel'
  'python-flask-limiter'
  'python-flask-login'
  'python-flask-principal'
  'python-flask-wtf'
  'python-pypdf2'
  'python-pytz'
  'python-requests'
  'python-sqlalchemy'
  'python-tornado'
  'python-wand'
  'python-unidecode'
  'python-blinker'
  'python-iso639'
  'python-lxml'
  'python-pycountry'
)
optdepends=(
  'python-jsonschema: kobo sync support'
)
provides=('calibre-web')
install=calibre-web.install
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/janeczku/calibre-web/archive/${pkgver}.tar.gz"
  'calibre-web.install'
  'calibre-web.service'
  'calibre-web.sysusers'
  'calibre-web.tmpfiles'
)
sha256sums=(
  '007bc4168c86894b1f4aa8331ffba1c717befe4449c266356cbed6ebd592d337'
  '91352616c004ab08590e892b1f8a74cf53fda876254699e3126021f557ea0231'
  'f2072ddcbd5753b75272b90d571c49b665506705d8da682210be64f908708e2c'
  'b5d6c225761244b6c698af0279c9501bb9514e3e2a5151ca6e3212a4f5d0d76a'
  '82fae88cbed6d69e5a04586293de81bdc55994a4fe003178c39b5a35f2c87c1a'
)

package() {
  cd $pkgname-$pkgver
  install -d -m 755 "${pkgdir}/usr/share/webapps/calibre-web"
  cp -a "${srcdir}/$pkgname-$pkgver"/* "${pkgdir}/usr/share/webapps/calibre-web/"
  rm -rf "${pkgdir}/usr/share/webapps/calibre-web/.git"

  install -D -m 644 "${srcdir}/calibre-web.service" "${pkgdir}/usr/lib/systemd/system/calibre-web.service"
  install -D -m 644 "${srcdir}/calibre-web.sysusers" "${pkgdir}/usr/lib/sysusers.d/calibre-web.conf"
  install -D -m 644 "${srcdir}/calibre-web.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/calibre-web.conf"
}

# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots/
# Based on PKGBUILD from AUR
# Contributor: gilbus <aur [at] tinkershell [dot] eu>

pkgbase=rssbridge
pkgname=rss-bridge
pkgver=2020_11_10
pkgrel=2
pkgdesc="The RSS feed for websites missing it"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://github.com/RSS-Bridge/rss-bridge/wiki"
license=("Unlicense")
depends=(php7)
backup=(
  "etc/webapps/$pkgname/config.ini.php"
  "etc/webapps/$pkgname/whitelist.txt"
  )
source=(
  "https://github.com/RSS-Bridge/$pkgname/archive/${pkgver//_/-}.tar.gz"
  "$pkgname.tmpfiles"
)
sha256sums=('345e5d947a700dd8e1c685513fd6b47ce0e031c3530940bc5ccb8e6cbf82d2a5'
            'f895b19c10923515a157df08c9cf1128c93cd3fe43afc935b9fa9647138f99f2')

package() {

  cd "$pkgname-${pkgver//_/-}"
  _instdir=$pkgdir/usr/share/webapps/$pkgname/

  # install project
  mkdir -p "$_instdir"
  cp -ra * "$_instdir/"

  # setup cache dir
  install -Dm644 "$srcdir/$pkgname.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  rm -r "$_instdir"/cache
  ln -sf /var/cache/$pkgname "$_instdir"/cache

  # link configs to /etc
  install -D config.default.ini.php $pkgdir/etc/webapps/$pkgname/config.ini.php
  install -D whitelist.default.txt $pkgdir/etc/webapps/$pkgname/whitelist.txt
  ln -s "/etc/webapps/$pkgname/config.ini.php" "$pkgdir/usr/share/webapps/$pkgname/config.ini.php"
  ln -s "/etc/webapps/$pkgname/whitelist.txt" "$pkgdir/usr/share/webapps/$pkgname/whitelist.txt"

  # fix permissions
  chown -R http:http $pkgdir/etc/webapps/$pkgname $pkgdir/usr/share/webapps/$pkgname/
  find "$pkgdir"/usr/share/webapps/${pkgname} -type f -exec chmod 0644 {} \;
  find "$pkgdir"/usr/share/webapps/${pkgname} -type d -exec chmod 0755 {} \;
}


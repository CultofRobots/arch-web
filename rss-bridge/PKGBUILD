# Maintainer: robot <cultofrobots [at] protonmail [dot] com>
# Website: https://github.com/CultofRobots/
# Based on PKGBUILD from AUR
# Contributor: gilbus <aur [at] tinkershell [dot] eu>

pkgbase=rssbridge
pkgname=rss-bridge
pkgver=2022_06_10
pkgrel=1
pkgdesc="The RSS feed for websites missing it"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://github.com/RSS-Bridge/rss-bridge/wiki"
license=("Unlicense")
depends=(php)
backup=(
  "etc/webapps/$pkgname/config.ini.php"
  "etc/webapps/$pkgname/whitelist.txt"
  )
source=(
  "https://github.com/RSS-Bridge/$pkgname/archive/${pkgver//_/-}.tar.gz"
  "$pkgname.sysusers"
  "$pkgname.tmpfiles"
)
sha256sums=('c70798cb569da40cf1cea899da28663b6fc7e0ee2bcae71ad1f31f1b592b4ccd'
            '42eb933a4632ada3247341d24f967c76cf363ff50e6ed13b39963983454f8020'
            '32bc1d3a61862fe87dd8b72b814feebe72e2d2b975e489e00d612fc607ae9d7a')
conflicts=("rss-bridge-git")

package() {

  cd "$pkgname-${pkgver//_/-}"
  _instdir=$pkgdir/usr/share/webapps/$pkgname/

  # install project
  mkdir -p "$_instdir"
  cp -ra * "$_instdir/"

  # move config to /etc
  install -D config.default.ini.php $pkgdir/etc/webapps/$pkgname/config.ini.php
  install -D whitelist.default.txt $pkgdir/etc/webapps/$pkgname/whitelist.txt
  ln -s "/etc/webapps/$pkgname/config.ini.php" "$pkgdir/usr/share/webapps/$pkgname/config.ini.php"
  ln -s "/etc/webapps/$pkgname/whitelist.txt" "$pkgdir/usr/share/webapps/$pkgname/whitelist.txt"

  # setup user and directory creation
  install -Dm644 "$srcdir/$pkgname.sysusers" "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -Dm644 "$srcdir/$pkgname.tmpfiles"  "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  rm -r "$_instdir"/cache
  ln -s /var/cache/$pkgname "$_instdir"/cache

  find "$pkgdir"/usr/share/webapps/${pkgname} -type f -exec chmod 0644 {} \;
  find "$pkgdir"/usr/share/webapps/${pkgname} -type d -exec chmod 0755 {} \;
}

